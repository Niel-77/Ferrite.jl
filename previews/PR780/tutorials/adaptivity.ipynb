{
 "cells": [
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using Ferrite, FerriteGmsh, SparseArrays\n",
    "#grid = togrid(\"l.msh\");\n",
    "grid = generate_grid(Quadrilateral,(2,2))\n",
    "grid  = ForestBWG(grid,25)\n",
    "Ferrite.refine_all!(grid,1)\n",
    "Ferrite.refine_all!(grid,2)\n",
    "\n",
    "struct Elasticity\n",
    "    G::Float64\n",
    "    K::Float64\n",
    "end\n",
    "\n",
    "function material_routine(material::Elasticity, ε::SymmetricTensor{2})\n",
    "    (; G, K) = material\n",
    "    stress(ε) = 2G * dev(ε) + K * tr(ε) * one(ε)\n",
    "    ∂σ∂ε, σ = gradient(stress, ε, :all)\n",
    "    return σ, ∂σ∂ε\n",
    "end\n",
    "\n",
    "E = 200e3 # Young's modulus [MPa]\n",
    "ν = 0.2 # Poisson's ratio [-]\n",
    "material = Elasticity(E/2(1+ν), E/3(1-2ν));\n",
    "\n",
    "function assemble_cell!(ke, fe, cellvalues, material, ue)\n",
    "    fill!(ke, 0.0)\n",
    "    fill!(fe, 0.0)\n",
    "\n",
    "    n_basefuncs = getnbasefunctions(cellvalues)\n",
    "    for q_point in 1:getnquadpoints(cellvalues)\n",
    "        # For each integration point, compute strain, stress and material stiffness\n",
    "        ε = function_symmetric_gradient(cellvalues, q_point, ue)\n",
    "        σ, ∂σ∂ε = material_routine(material, ε)\n",
    "\n",
    "        dΩ = getdetJdV(cellvalues, q_point)\n",
    "        for i in 1:n_basefuncs\n",
    "            ∇Nᵢ = shape_gradient(cellvalues, q_point, i)\n",
    "            fe[i] += σ ⊡ ∇Nᵢ * dΩ\n",
    "            for j in 1:n_basefuncs\n",
    "                ∇ˢʸᵐNⱼ = shape_symmetric_gradient(cellvalues, q_point, j)\n",
    "                ke[i, j] += (∂σ∂ε ⊡ ∇ˢʸᵐNⱼ) ⊡ ∇Nᵢ * dΩ\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "function assemble_global!(K, f, a, dh, cellvalues, material)\n",
    "    # Allocate the element stiffness matrix and element force vector\n",
    "    n_basefuncs = getnbasefunctions(cellvalues)\n",
    "    ke = zeros(n_basefuncs, n_basefuncs)\n",
    "    fe = zeros(n_basefuncs)\n",
    "    # Create an assembler\n",
    "    assembler = start_assemble(K, f)\n",
    "    # Loop over all cells\n",
    "    for cell in CellIterator(dh)\n",
    "        reinit!(cellvalues, cell)\n",
    "        @views ue = a[celldofs(cell)]\n",
    "        # Compute element contribution\n",
    "        assemble_cell!(ke, fe, cellvalues, material, ue)\n",
    "        # Assemble ke and fe into K and f\n",
    "        assemble!(assembler, celldofs(cell), ke, fe)\n",
    "    end\n",
    "    return K, f\n",
    "end\n",
    "\n",
    "function solve(grid)\n",
    "    dim = 2\n",
    "    order = 1\n",
    "    ip = Lagrange{RefQuadrilateral, order}()^dim\n",
    "    qr = QuadratureRule{RefQuadrilateral}(2)\n",
    "    cellvalues = CellValues(qr, ip);\n",
    "\n",
    "    dh = DofHandler(grid)\n",
    "    add!(dh, :u, ip)\n",
    "    close!(dh);\n",
    "\n",
    "    ch = ConstraintHandler(dh)\n",
    "    add!(ch, Ferrite.ConformityConstraint(:u))\n",
    "    add!(ch, Dirichlet(:u, getfacetset(grid, \"top\"), (x, t) -> Vec{2}((0.0,0.0)), [1,2]))\n",
    "    add!(ch, Dirichlet(:u, getfacetset(grid, \"right\"), (x, t) -> 0.01, 2))\n",
    "    close!(ch);\n",
    "\n",
    "    K = create_sparsity_pattern(dh,ch)\n",
    "    f = zeros(ndofs(dh))\n",
    "    a = zeros(ndofs(dh))\n",
    "    assemble_global!(K, f, a, dh, cellvalues, material);\n",
    "    apply!(K, f, ch)\n",
    "    u = K \\ f;\n",
    "    apply!(u,ch)\n",
    "    return u,dh,ch,cellvalues\n",
    "end\n",
    "\n",
    "function compute_fluxes(u,dh)\n",
    "    ip = Lagrange{RefQuadrilateral, 1}()^2\n",
    "    # Superconvergent points\n",
    "    qr = QuadratureRule{RefQuadrilateral}(1)\n",
    "    cellvalues_sc = CellValues(qr, ip);\n",
    "    # \"Normal\" quadrature points for the fluxes\n",
    "    qr = QuadratureRule{RefQuadrilateral}(2)\n",
    "    cellvalues = CellValues(qr, ip);\n",
    "    # Buffers\n",
    "    σ_gp_sc = Vector{Vector{SymmetricTensor{2,2,Float64,3}}}()\n",
    "    σ_gp_sc_loc = Vector{SymmetricTensor{2,2,Float64,3}}()\n",
    "    σ_gp = Vector{Vector{SymmetricTensor{2,2,Float64,3}}}()\n",
    "    σ_gp_loc = Vector{SymmetricTensor{2,2,Float64,3}}()\n",
    "    for (cellid,cell) in enumerate(CellIterator(dh))\n",
    "        reinit!(cellvalues, cell)\n",
    "        reinit!(cellvalues_sc, cell)\n",
    "        @views ue = u[celldofs(cell)]\n",
    "        for q_point in 1:getnquadpoints(cellvalues)\n",
    "            ε = function_symmetric_gradient(cellvalues, q_point, ue)\n",
    "            σ, _ = material_routine(material, ε)\n",
    "            push!(σ_gp_loc, σ)\n",
    "        end\n",
    "        for q_point in 1:getnquadpoints(cellvalues_sc)\n",
    "            ε = function_symmetric_gradient(cellvalues_sc, q_point, ue)\n",
    "            σ, _ = material_routine(material, ε)\n",
    "            push!(σ_gp_sc_loc, σ)\n",
    "        end\n",
    "        push!(σ_gp,copy(σ_gp_loc))\n",
    "        push!(σ_gp_sc,copy(σ_gp_sc_loc))\n",
    "        # Reset buffer for local points\n",
    "        empty!(σ_gp_loc)\n",
    "        empty!(σ_gp_sc_loc)\n",
    "    end\n",
    "    return σ_gp, σ_gp_sc\n",
    "end\n",
    "\n",
    "function solve_adaptive(initial_grid)\n",
    "    ip = Lagrange{RefQuadrilateral, 1}()\n",
    "    qr = QuadratureRule{RefQuadrilateral}(1)\n",
    "    cellvalues_tensorial = CellValues(qr, ip);\n",
    "    finished = false\n",
    "    i = 1\n",
    "    grid = initial_grid\n",
    "    while !finished\n",
    "        transfered_grid = Ferrite.creategrid(grid)\n",
    "        u,dh,ch,cv = solve(transfered_grid)\n",
    "        σ_gp, σ_gp_sc = compute_fluxes(u,dh)\n",
    "        projector = L2Projector(Lagrange{RefQuadrilateral, 1}()^2, transfered_grid)\n",
    "        σ_dof = project(projector, σ_gp, QuadratureRule{RefQuadrilateral}(2))\n",
    "        cells_to_refine = Int[]\n",
    "        error_arr = Float64[]\n",
    "        for (cellid,cell) in enumerate(CellIterator(projector.dh))\n",
    "            reinit!(cellvalues_tensorial, cell)\n",
    "            @views σe = σ_dof[celldofs(cell)]\n",
    "            error = 0.0\n",
    "            for q_point in 1:getnquadpoints(cellvalues_tensorial)\n",
    "                σ_dof_at_sc = function_value(cellvalues_tensorial, q_point, σe)\n",
    "                error += norm((σ_gp_sc[cellid][1] - σ_dof_at_sc )) * getdetJdV(cellvalues_tensorial,q_point)\n",
    "            end\n",
    "            if error > 0.01\n",
    "                push!(cells_to_refine,cellid)\n",
    "            end\n",
    "            push!(error_arr,error)\n",
    "        end\n",
    "        VTKFile(\"linear_elasticity-$i\", dh) do vtk\n",
    "            write_solution(vtk, dh, u)\n",
    "            write_projection(vtk, projector, σ_dof, \"stress\")\n",
    "            write_cell_data(vtk, getindex.(collect(Iterators.flatten(σ_gp_sc)),1), \"stress sc\")\n",
    "            write_cell_data(vtk, error_arr, \"error\")\n",
    "        end\n",
    "\n",
    "        Ferrite.refine!(grid,cells_to_refine)\n",
    "        VTKFile(\"unbalanced.vtu\", dh) do vtk\n",
    "        end\n",
    "\n",
    "        Ferrite.balanceforest!(grid)\n",
    "        VTKFile(\"balanced.vtu\", dh) do vtk\n",
    "        end\n",
    "\n",
    "        i += 1\n",
    "        if isempty(cells_to_refine)\n",
    "            finished = true\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "solve_adaptive(grid)"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.3"
  },
  "kernelspec": {
   "name": "julia-1.10",
   "display_name": "Julia 1.10.3",
   "language": "julia"
  }
 },
 "nbformat": 4
}
