{
 "cells": [
  {
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "i = 1\n",
      "i = 2\n",
      "i = 3\n",
      "i = 4\n",
      "i = 5\n",
      "i = 6\n",
      "i = 7\n",
      "i = 8\n"
     ]
    },
    {
     "output_type": "execute_result",
     "data": {
      "text/plain": "9-element Vector{String}:\n \"heat_amr.pvd\"\n \"heat_amr_1.vtu\"\n \"heat_amr_2.vtu\"\n \"heat_amr_3.vtu\"\n \"heat_amr_4.vtu\"\n \"heat_amr_5.vtu\"\n \"heat_amr_6.vtu\"\n \"heat_amr_7.vtu\"\n \"heat_amr_8.vtu\""
     },
     "metadata": {},
     "execution_count": 1
    }
   ],
   "cell_type": "code",
   "source": [
    "using Ferrite, FerriteGmsh, SparseArrays\n",
    "grid = generate_grid(Quadrilateral, (8,8));\n",
    "function random_deformation_field(x)\n",
    "    if any(x .≈ -1.0) || any(x .≈ 1.0)\n",
    "        return x\n",
    "    else\n",
    "        Vec{2}(x .+ (rand(2).-0.5)*0.15)\n",
    "    end\n",
    "end\n",
    "transform_coordinates!(grid, random_deformation_field)\n",
    "grid  = ForestBWG(grid,10)\n",
    "\n",
    "analytical_solution(x) = atan(2*(norm(x)-0.5)/0.02)\n",
    "analytical_rhs(x) = -laplace(analytical_solution,x)\n",
    "\n",
    "function assemble_cell!(ke, fe, cellvalues, ue, coords)\n",
    "    fill!(ke, 0.0)\n",
    "    fill!(fe, 0.0)\n",
    "\n",
    "    n_basefuncs = getnbasefunctions(cellvalues)\n",
    "    for q_point in 1:getnquadpoints(cellvalues)\n",
    "        x = spatial_coordinate(cellvalues, q_point, coords)\n",
    "        dΩ = getdetJdV(cellvalues, q_point)\n",
    "        for i in 1:n_basefuncs\n",
    "            Nᵢ = shape_value(cellvalues, q_point, i)\n",
    "            ∇Nᵢ = shape_gradient(cellvalues, q_point, i)\n",
    "            fe[i] += analytical_rhs(x) * Nᵢ * dΩ\n",
    "            for j in 1:n_basefuncs\n",
    "                ∇Nⱼ = shape_gradient(cellvalues, q_point, j)\n",
    "                ke[i, j] += ∇Nⱼ ⋅ ∇Nᵢ * dΩ\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "function assemble_global!(K, f, a, dh, cellvalues)\n",
    "    # Allocate the element stiffness matrix and element force vector\n",
    "    n_basefuncs = getnbasefunctions(cellvalues)\n",
    "    ke = zeros(n_basefuncs, n_basefuncs)\n",
    "    fe = zeros(n_basefuncs)\n",
    "    # Create an assembler\n",
    "    assembler = start_assemble(K, f)\n",
    "    # Loop over all cells\n",
    "    for cell in CellIterator(dh)\n",
    "        reinit!(cellvalues, cell)\n",
    "        @views ue = a[celldofs(cell)]\n",
    "        # Compute element contribution\n",
    "        coords = getcoordinates(cell)\n",
    "        assemble_cell!(ke, fe, cellvalues, ue, coords)\n",
    "        # Assemble ke and fe into K and f\n",
    "        assemble!(assembler, celldofs(cell), ke, fe)\n",
    "    end\n",
    "    return K, f\n",
    "end\n",
    "\n",
    "function solve(grid)\n",
    "    dim = 2\n",
    "    order = 1\n",
    "    ip = Lagrange{RefQuadrilateral, order}()\n",
    "    qr = QuadratureRule{RefQuadrilateral}(2)\n",
    "    cellvalues = CellValues(qr, ip);\n",
    "\n",
    "    dh = DofHandler(grid)\n",
    "    add!(dh, :u, ip)\n",
    "    close!(dh);\n",
    "\n",
    "    ch = ConstraintHandler(dh)\n",
    "    add!(ch, ConformityConstraint(:u))\n",
    "    add!(ch, Dirichlet(:u, getfacetset(grid, \"top\"), (x, t) -> 0.0))\n",
    "    add!(ch, Dirichlet(:u, getfacetset(grid, \"right\"), (x, t) -> 0.0))\n",
    "    add!(ch, Dirichlet(:u, getfacetset(grid, \"left\"), (x, t) -> 0.0))\n",
    "    add!(ch, Dirichlet(:u, getfacetset(grid, \"bottom\"), (x, t) -> 0.0))\n",
    "    close!(ch);\n",
    "\n",
    "    K = create_sparsity_pattern(dh,ch)\n",
    "    f = zeros(ndofs(dh))\n",
    "    a = zeros(ndofs(dh))\n",
    "    assemble_global!(K, f, a, dh, cellvalues);\n",
    "    apply!(K, f, ch)\n",
    "    u = K \\ f;\n",
    "    apply!(u,ch)\n",
    "    return u,dh,ch,cellvalues\n",
    "end\n",
    "\n",
    "function compute_fluxes(u,dh)\n",
    "    ip = Lagrange{RefQuadrilateral, 1}()\n",
    "    # Normal quadrature points\n",
    "    qr = QuadratureRule{RefQuadrilateral}(2)\n",
    "    cellvalues = CellValues(qr, ip);\n",
    "    # Superconvergent point\n",
    "    qr_sc = QuadratureRule{RefQuadrilateral}(1)\n",
    "    cellvalues_sc = CellValues(qr_sc, ip);\n",
    "    # Buffers\n",
    "    σ_gp = Vector{Vector{Vec{2,Float64}}}()\n",
    "    σ_gp_loc = Vector{Vec{2,Float64}}()\n",
    "    σ_gp_sc = Vector{Vector{Vec{2,Float64}}}()\n",
    "    σ_gp_sc_loc = Vector{Vec{2,Float64}}()\n",
    "    for (cellid,cell) in enumerate(CellIterator(dh))\n",
    "        @views ue = u[celldofs(cell)]\n",
    "\n",
    "        reinit!(cellvalues, cell)\n",
    "        for q_point in 1:getnquadpoints(cellvalues)\n",
    "            gradu = function_gradient(cellvalues, q_point, ue)\n",
    "            push!(σ_gp_loc, gradu)\n",
    "        end\n",
    "        push!(σ_gp,copy(σ_gp_loc))\n",
    "        empty!(σ_gp_loc)\n",
    "\n",
    "        reinit!(cellvalues_sc, cell)\n",
    "        for q_point in 1:getnquadpoints(cellvalues_sc)\n",
    "            gradu = function_gradient(cellvalues_sc, q_point, ue)\n",
    "            push!(σ_gp_sc_loc, gradu)\n",
    "        end\n",
    "        push!(σ_gp_sc,copy(σ_gp_sc_loc))\n",
    "        empty!(σ_gp_sc_loc)\n",
    "    end\n",
    "    return σ_gp, σ_gp_sc\n",
    "end\n",
    "\n",
    "function solve_adaptive(initial_grid)\n",
    "    ip = Lagrange{RefQuadrilateral, 1}()^2\n",
    "    qr_sc = QuadratureRule{RefQuadrilateral}(1)\n",
    "    cellvalues_flux = CellValues(qr_sc, ip);\n",
    "    finished = false\n",
    "    i = 1\n",
    "    grid = deepcopy(initial_grid)\n",
    "    pvd = VTKFileCollection(\"heat_amr.pvd\",grid);\n",
    "    while !finished && i<=10\n",
    "        @show i\n",
    "        transfered_grid = Ferrite.creategrid(grid)\n",
    "        u,dh,ch,cv = solve(transfered_grid)\n",
    "        σ_gp, σ_gp_sc = compute_fluxes(u,dh)\n",
    "        projector = L2Projector(Lagrange{RefQuadrilateral, 1}(), transfered_grid)\n",
    "        σ_dof = project(projector, σ_gp, QuadratureRule{RefQuadrilateral}(2))\n",
    "        cells_to_refine = Int[]\n",
    "        error_arr = Float64[]\n",
    "        for (cellid,cell) in enumerate(CellIterator(projector.dh))\n",
    "            reinit!(cellvalues_flux, cell)\n",
    "            @views σe = σ_dof[celldofs(cell)]\n",
    "            error = 0.0\n",
    "            for q_point in 1:getnquadpoints(cellvalues_flux)\n",
    "                σ_dof_at_sc = function_value(cellvalues_flux, q_point, σe)\n",
    "                error += norm((σ_gp_sc[cellid][q_point] - σ_dof_at_sc ))\n",
    "                error *= getdetJdV(cellvalues_flux,q_point)\n",
    "            end\n",
    "            if error > 0.001\n",
    "                push!(cells_to_refine,cellid)\n",
    "            end\n",
    "            push!(error_arr,error)\n",
    "        end\n",
    "\n",
    "        addstep!(pvd, i, dh) do vtk\n",
    "            write_solution(vtk, dh, u)\n",
    "            write_projection(vtk, projector, σ_dof, \"flux\")\n",
    "            write_cell_data(vtk, getindex.(collect(Iterators.flatten(σ_gp_sc)),1), \"flux sc x\")\n",
    "            write_cell_data(vtk, getindex.(collect(Iterators.flatten(σ_gp_sc)),2), \"flux sc y\")\n",
    "            write_cell_data(vtk, error_arr, \"error\")\n",
    "        end\n",
    "\n",
    "        Ferrite.refine!(grid, cells_to_refine)\n",
    "        Ferrite.balanceforest!(grid)\n",
    "\n",
    "        i += 1\n",
    "        if isempty(cells_to_refine)\n",
    "            finished = true\n",
    "        end\n",
    "    end\n",
    "    close(pvd);\n",
    "end\n",
    "\n",
    "solve_adaptive(grid)"
   ],
   "metadata": {},
   "execution_count": 1
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.3"
  },
  "kernelspec": {
   "name": "julia-1.10",
   "display_name": "Julia 1.10.3",
   "language": "julia"
  }
 },
 "nbformat": 4
}
